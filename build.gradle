subprojects {
  apply plugin: 'java'
  apply plugin: 'jacoco'
  apply plugin: 'findbugs'
  apply plugin: 'pmd'

  version = '1.0'
  
  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  repositories {
     mavenCentral()
  }
  
  dependencies {
    testCompile 'junit:junit:4.12'
  }

  /* JaCoCo */
 
  jacoco {
    toolVersion = "0.7.6.201602180812"
  }
  
  jacocoTestReport {
    reports {
      xml.enabled true
      html.destination "${buildDir}/reports/jacoco-html"
    }
  }
  
  check.dependsOn jacocoTestReport
 
  /* Findbugs */
  
  findbugs {
    ignoreFailures = true
  }
  
  tasks.withType(FindBugs) {
    reports {
      xml.enabled false
      html.enabled true
    }
  }
 
  /* PMD */

  pmd {
    ignoreFailures = true
    reportsDir = file("${buildDir}/reports/pmd")
    ruleSets = ["java-basic", "java-braces"]
  }
  
  /* Tests */
  
  test {
    reports {
      junitXml.enabled true
      junitXml.destination = "${buildDir}/reports/junit-xml"
      html.enabled true
      html.destination = "${buildDir}/reports/junit-html"
    }
  }
  
  tasks.withType(Test) {
    maxHeapSize = "512m"

    jvmArgs "-XX:MaxPermSize=512m"
    
	/* Run tests in parallel */
	maxParallelForks = Runtime.runtime.availableProcessors()
  }
}

project('org.s23m.cell.platform') {
  dependencies {
    compile project(':org.s23m.cell.kernel')
  }
}

project('org.s23m.cell.persistence') {
  dependencies {
    compile project(':org.s23m.cell.kernel')
  }
}